-module(wfcli_incident_log_eunit).
-include_lib("eunit/include/eunit.hrl").

persistent_bounded_warning_log_test() ->
    with_log(fun(Path) ->
        ?assertEqual(ok, wfcli_incident_log:install()),
        {ok, #{level := warning, config := Config}} = logger:get_handler_config(wfdaemon_incidents),
        ?assertEqual(1024 * 1024, maps:get(max_no_bytes, Config)),
        ?assertEqual(2, maps:get(max_no_files, Config)),
        logger:notice("wfcli_notice_not_an_incident"),
        logger:warning(#{event => wfcli_test_incident, source => wfcd, reason => timeout}),
        ok = logger_std_h:filesync(wfdaemon_incidents),
        {ok, Body} = file:read_file(Path),
        ?assertNotEqual(nomatch, binary:match(Body, <<"wfcli_test_incident">>)),
        ?assertEqual(nomatch, binary:match(Body, <<"wfcli_notice_not_an_incident">>))
    end).

manual_refresh_failure_is_persisted_test() ->
    with_log(fun(Path) ->
        application:set_env(wfdaemon, source_refresh_interval_ms, 0),
        application:set_env(wfdaemon, source_update_fun, fun(_) -> {error, fixture_failure} end),
        {ok, Pid} = wfcli_source_manager:start_link(),
        try
            {ok, Ref} = wfcli_source_manager:submit(self(), #{action => refresh, selections => [wfcd]}),
            receive
                {wfcli_daemon, Ref, {ok, #{success := false}}} -> ok
            after 1000 -> ?assert(false)
            end,
            ok = logger_std_h:filesync(wfdaemon_incidents),
            {ok, Body} = file:read_file(Path),
            ?assertNotEqual(nomatch, binary:match(Body, <<"knowledge_refresh_failed">>)),
            ?assertNotEqual(nomatch, binary:match(Body, <<"fixture_failure">>)),
            ?assertEqual(1, length(binary:matches(Body, <<"knowledge_refresh_failed">>)))
        after
            gen_server:stop(Pid),
            application:unset_env(wfdaemon, source_refresh_interval_ms),
            application:unset_env(wfdaemon, source_update_fun)
        end
    end).

background_refresh_failure_is_persisted_once_test() ->
    with_log(fun(Path) ->
        application:set_env(wfdaemon, source_refresh_interval_ms, 0),
        application:set_env(wfdaemon, source_max_age_seconds, 0),
        application:set_env(wfdaemon, source_update_fun, fun(wfcd) -> {error, fixture_failure}; (_) -> ok end),
        {ok, Pid} = wfcli_source_manager:start_link(),
        try
            Pid ! refresh_stale_sources,
            {ok, Ref} = wfcli_source_manager:submit(self(), #{action => refresh, selections => []}),
            receive {wfcli_daemon, Ref, {ok, #{success := true}}} -> ok after 1000 -> ?assert(false) end,
            ok = logger_std_h:filesync(wfdaemon_incidents),
            {ok, Body} = file:read_file(Path),
            ?assertEqual(1, length(binary:matches(Body, <<"knowledge_refresh_failed">>))),
            ?assertNotEqual(nomatch, binary:match(Body, <<"fixture_failure">>))
        after
            gen_server:stop(Pid),
            application:unset_env(wfdaemon, source_refresh_interval_ms),
            application:unset_env(wfdaemon, source_max_age_seconds),
            application:unset_env(wfdaemon, source_update_fun)
        end
    end).

mirror_failure_is_persisted_test() ->
    with_log(fun(Path) ->
        application:set_env(wfdaemon, wfcd_http_fun,
            fun("https://registry.npmjs.org/@wfcd/items/latest", _) ->
                    {ok, 200, <<"{\"name\":\"@wfcd/items\",\"version\":\"1.2.3\"}">>};
               (_, _) -> {ok, 503, <<>>}
            end),
        try
            ?assertMatch({error, {wfcd_sources_failed, _, _}}, wfcli_wfcd:fetch()),
            ok = logger_std_h:filesync(wfdaemon_incidents),
            {ok, Body} = file:read_file(Path),
            ?assertEqual(2, length(binary:matches(Body, <<"wfcd_mirror_failed">>))),
            ?assertNotEqual(nomatch, binary:match(Body, <<"unpkg">>)),
            ?assertNotEqual(nomatch, binary:match(Body, <<"jsdelivr">>))
        after application:unset_env(wfdaemon, wfcd_http_fun)
        end
    end).

with_log(Test) ->
    PreviousHandler = logger:get_handler_config(wfdaemon_incidents),
    _ = logger:remove_handler(wfdaemon_incidents),
    Root = filename:join("/tmp", "wfcli-incident-test-" ++
                         integer_to_list(erlang:unique_integer([positive]))),
    Previous = os:getenv("XDG_STATE_HOME"),
    os:putenv("XDG_STATE_HOME", Root),
    try
        ok = wfcli_incident_log:install(),
        Test(wfcli_paths:state_file("wfdaemon.log"))
    after
        logger:remove_handler(wfdaemon_incidents),
        case Previous of false -> os:unsetenv("XDG_STATE_HOME"); _ -> os:putenv("XDG_STATE_HOME", Previous) end,
        file:del_dir_r(Root),
        case PreviousHandler of
            {ok, #{module := Module} = Config} ->
                logger:add_handler(wfdaemon_incidents, Module, maps:without([id, module], Config));
            _ -> ok
        end
    end.
