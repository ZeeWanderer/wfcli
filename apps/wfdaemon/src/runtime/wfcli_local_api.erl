%%%-------------------------------------------------------------------
%% User-local Unix socket API for native companions.
%%%-------------------------------------------------------------------
-module(wfcli_local_api).

-behaviour(gen_server).

-export([start_link/0, status/0, socket_path/0, companion_command/1]).
-export([init/1, handle_call/3, handle_cast/2, handle_info/2,
         terminate/2, code_change/3]).

-define(SERVER, ?MODULE).
-define(MAX_BUFFER_BYTES, 8388608).
-define(MAX_MARKET_RESOLVE_LABELS, 20).
-define(MAX_MARKET_RESOLVE_LIMIT, 5).
-define(DEFAULT_REQUEST_WORKERS, 4).
-define(DEFAULT_GLOBAL_REQUEST_WORKERS, 16).

-type state() :: #{
    listen := socket:socket(),
    path := file:filename_all(),
    acceptor := pid(),
    connections := map(),
    monitors := map(),
    worker_holders := map(),
    worker_waiters := queue:queue(),
    worker_count := non_neg_integer(),
    worker_limit := pos_integer()
}.

-doc "Start owner-only Unix socket endpoint used by native companion processes.".
-spec start_link() -> {ok, pid()} | ignore | {error, term()}.
start_link() ->
    gen_server:start_link({local, ?SERVER}, ?MODULE, [], []).

-doc "Return local API socket and connected-client state.".
-spec status() -> map().
status() ->
    gen_server:call(?SERVER, status).

-doc "Return configured native companion Unix socket path.".
-spec socket_path() -> file:filename_all().
socket_path() ->
    case application:get_env(wfdaemon, local_socket) of
        {ok, Path} -> Path;
        undefined ->
            case os:getenv("WFCLI_DAEMON_SOCKET") of
                false -> wfcli_paths:runtime_file("wfdaemon.sock");
                "" -> wfcli_paths:runtime_file("wfdaemon.sock");
                Path -> Path
            end
    end.

-doc "Send an optional diagnostic command to every connected companion.".
-spec companion_command(map()) -> {ok, non_neg_integer()}.
companion_command(Command) when is_map(Command) ->
    gen_server:call(?SERVER, {companion_command, Command}).

-spec init([]) -> {ok, state()} | {stop, term()}.
init([]) ->
    process_flag(trap_exit, true),
    Path = socket_path(),
    case open_listener(Path) of
        {ok, Listen} ->
            Parent = self(),
            Acceptor = spawn_link(fun() -> accept_loop(Listen, Parent) end),
            {ok, #{listen => Listen, path => Path, acceptor => Acceptor,
                   connections => #{}, monitors => #{}, worker_holders => #{},
                   worker_waiters => queue:new(), worker_count => 0,
                   worker_limit => global_local_worker_limit()}};
        {error, Reason} ->
            {stop, {local_api_listen_failed, Path, Reason}}
    end.

-spec handle_call(term(), gen_server:from(), state()) -> {reply, term(), state()}.
handle_call(status, _From, State) ->
    Connections = maps:get(connections, State),
    CompanionDetails =
        [maps:without([monitor], Info)
         || {_Pid, Info} <- maps:to_list(Connections),
            maps:get(client, Info, undefined) =:= <<"wfcompanion">>],
    {reply, #{socket => maps:get(path, State),
              connections => map_size(Connections),
              companions => length(CompanionDetails),
              companion_details => CompanionDetails,
              local_workers => maps:get(worker_count, State, 0),
              local_workers_queued => queue:len(
                                        maps:get(worker_waiters, State, queue:new())),
              local_worker_limit => maps:get(worker_limit, State,
                                             global_local_worker_limit()),
              protocol => wfcli_local_protocol:protocol_version()}, State};
handle_call({acquire_local_workers, Pid, Count}, _From, State)
  when is_pid(Pid), is_integer(Count), Count > 0 ->
    {Granted, Queued, State1} = acquire_local_workers(Pid, Count, State),
    {reply, {Granted, Queued}, State1};
handle_call({companion_command, Command}, _From, State) ->
    Count = maps:fold(
      fun(Pid, Info, Acc) ->
          case maps:get(client, Info, undefined) of
              <<"wfcompanion">> -> Pid ! {daemon_command, Command}, Acc + 1;
              _ -> Acc
          end
      end,
      0,
      maps:get(connections, State)),
    {reply, {ok, Count}, State};
handle_call(Request, _From, State) ->
    {reply, {error, {unknown_request, Request}}, State}.

-spec handle_cast(term(), state()) -> {noreply, state()}.
handle_cast({release_local_worker, Pid}, State) when is_pid(Pid) ->
    {noreply, assign_local_worker_waiters(release_local_workers(Pid, 1, State))};
handle_cast(_Message, State) ->
    {noreply, State}.

-spec handle_info(term(), state()) -> {noreply, state()}.
handle_info({connection_started, Pid}, State) ->
    Monitor = erlang:monitor(process, Pid),
    Connections = maps:get(connections, State),
    Monitors = maps:get(monitors, State),
    {noreply, State#{connections => Connections#{Pid => #{monitor => Monitor}},
                     monitors => Monitors#{Monitor => Pid}}};
handle_info({client_identified, Pid, ClientInfo}, State) ->
    Connections = maps:get(connections, State),
    case maps:get(Pid, Connections, undefined) of
        undefined -> {noreply, State};
        Info ->
            Client = maps:get(client, ClientInfo),
            update_client_activity(Pid, maps:get(client, Info, undefined), Client),
            {noreply, State#{connections => Connections#{Pid => maps:merge(Info, ClientInfo)}}}
    end;
handle_info({'DOWN', Monitor, process, _Pid, _Reason}, State) ->
    case maps:take(Monitor, maps:get(monitors, State)) of
        error -> {noreply, State};
        {Connection, Monitors} ->
            Connections0 = maps:get(connections, State),
            case maps:take(Connection, Connections0) of
                error ->
                    {noreply, State#{monitors => Monitors}};
                {Info, Connections} ->
                    release_client_activity(Connection, Info),
                    State1 = drop_local_worker_client(Connection, State),
                    {noreply, assign_local_worker_waiters(
                                State1#{connections => Connections,
                                        monitors => Monitors})}
            end
    end;
handle_info({'EXIT', Acceptor, Reason}, State = #{acceptor := Acceptor}) ->
    case Reason of
        normal -> {noreply, State};
        _ ->
            Listen = maps:get(listen, State),
            Parent = self(),
            NewAcceptor = spawn_link(fun() -> accept_loop(Listen, Parent) end),
            {noreply, State#{acceptor => NewAcceptor}}
    end;
handle_info(_Message, State) ->
    {noreply, State}.

-spec terminate(term(), state()) -> ok.
terminate(_Reason, State) ->
    maps:foreach(fun(Pid, _Info) -> Pid ! shutdown end,
                 maps:get(connections, State, #{})),
    _ = socket:close(maps:get(listen, State)),
    _ = file:delete(maps:get(path, State)),
    ok.

-spec code_change(term(), state(), term()) -> {ok, state()}.
code_change(_OldVsn, State, _Extra) ->
    {ok, State#{worker_holders => maps:get(worker_holders, State, #{}),
                worker_waiters => maps:get(worker_waiters, State, queue:new()),
                worker_count => maps:get(worker_count, State, 0),
                worker_limit => maps:get(worker_limit, State,
                                         global_local_worker_limit())}}.

acquire_local_workers(Pid, Count, State) ->
    Available = max(0, maps:get(worker_limit, State) - maps:get(worker_count, State)),
    Granted = min(Count, Available),
    Queued = Count - Granted,
    State1 = add_local_worker_holders(Pid, Granted, State),
    Waiters = enqueue_local_worker_waiters(Pid, Queued,
                                            maps:get(worker_waiters, State1)),
    {Granted, Queued, State1#{worker_waiters => Waiters}}.

enqueue_local_worker_waiters(_Pid, 0, Queue) -> Queue;
enqueue_local_worker_waiters(Pid, Count, Queue) ->
    enqueue_local_worker_waiters(Pid, Count - 1, queue:in(Pid, Queue)).

add_local_worker_holders(_Pid, 0, State) -> State;
add_local_worker_holders(Pid, Count, State) ->
    Holders = maps:get(worker_holders, State),
    Held = maps:get(Pid, Holders, 0),
    State#{worker_holders => Holders#{Pid => Held + Count},
           worker_count => maps:get(worker_count, State) + Count}.

release_local_workers(Pid, Count, State) ->
    Holders = maps:get(worker_holders, State),
    Held = maps:get(Pid, Holders, 0),
    Released = min(Count, Held),
    Remaining = Held - Released,
    Holders1 = case Remaining of
        0 -> maps:remove(Pid, Holders);
        _ -> Holders#{Pid => Remaining}
    end,
    State#{worker_holders => Holders1,
           worker_count => maps:get(worker_count, State) - Released}.

drop_local_worker_client(Pid, State) ->
    Held = maps:get(Pid, maps:get(worker_holders, State), 0),
    Waiters = queue:filter(fun(WaitingPid) -> WaitingPid =/= Pid end,
                           maps:get(worker_waiters, State)),
    (release_local_workers(Pid, Held, State))#{worker_waiters => Waiters}.

assign_local_worker_waiters(State) ->
    case maps:get(worker_count, State) < maps:get(worker_limit, State) of
        false -> State;
        true ->
            case queue:out(maps:get(worker_waiters, State)) of
                {empty, Queue} -> State#{worker_waiters => Queue};
                {{value, Pid}, Queue} ->
                    State1 = State#{worker_waiters => Queue},
                    case maps:is_key(Pid, maps:get(connections, State1)) of
                        true ->
                            Pid ! local_worker_permit,
                            assign_local_worker_waiters(
                              add_local_worker_holders(Pid, 1, State1));
                        false -> assign_local_worker_waiters(State1)
                    end
            end
    end.

global_local_worker_limit() ->
    case application:get_env(wfdaemon, local_request_global_workers,
                             ?DEFAULT_GLOBAL_REQUEST_WORKERS) of
        Count when is_integer(Count), Count > 0 -> Count;
        _ -> ?DEFAULT_GLOBAL_REQUEST_WORKERS
    end.

open_listener(Path) ->
    case filelib:ensure_dir(Path) of
        ok -> open_listener_path(Path);
        {error, _Reason} = Error -> Error
    end.

open_listener_path(Path) ->
    _ = file:make_dir(filename:dirname(Path)),
    _ = file:change_mode(filename:dirname(Path), 8#700),
    _ = file:delete(Path),
    case socket:open(local, stream, default) of
        {ok, Listen} ->
            case socket:bind(Listen, #{family => local, path => Path}) of
                ok ->
                    case socket:listen(Listen) of
                        ok ->
                            _ = file:change_mode(Path, 8#600),
                            {ok, Listen};
                        {error, Reason} ->
                            _ = socket:close(Listen),
                            {error, Reason}
                    end;
                {error, Reason} ->
                    _ = socket:close(Listen),
                    {error, Reason}
            end;
        {error, _Reason} = Error -> Error
    end.

accept_loop(Listen, Parent) ->
    case socket:accept(Listen) of
        {ok, Socket} ->
            Handler = spawn(fun() -> await_socket(Parent) end),
            case socket:setopt(Socket, otp, controlling_process, Handler) of
                ok ->
                    Handler ! {accepted_socket, Socket},
                    Parent ! {connection_started, Handler};
                {error, Reason} ->
                    exit(Handler, {socket_transfer_failed, Reason}),
                    _ = socket:close(Socket)
            end,
            accept_loop(Listen, Parent);
        {error, closed} -> ok;
        {error, Reason} -> exit({accept_failed, Reason})
    end.

await_socket(Parent) ->
    receive
        {accepted_socket, Socket} -> connection(Socket, Parent)
    after 5000 ->
        exit(socket_transfer_timeout)
    end.

connection(Socket, Parent) ->
    process_flag(trap_exit, true),
    Handler = self(),
    {Reader, ReaderMonitor} = spawn_monitor(fun() -> socket_reader(Socket, Handler) end),
    State = #{socket => Socket, parent => Parent, reader => Reader,
              reader_monitor => ReaderMonitor, buffer => <<>>,
              hello => false, subscriptions => #{}, refs => #{}, market_refs => #{},
              market_account_refs => #{},
              market_presence_ref => undefined,
              activity_refs => #{},
              workers => #{}, worker_queue => queue:new(),
              worker_permits => 0, worker_permit_requests => 0,
              max_workers => local_worker_limit()},
    try
        FinalState = connection_loop(State),
        cleanup_connection(FinalState)
    after
        _ = socket:close(Socket)
    end.

connection_loop(State = #{reader_monitor := ReaderMonitor}) ->
    receive
        {socket_data, Data} ->
            case consume_data(Data, State) of
                {ok, State1} -> connection_loop(State1);
                {stop, _Reason, State1} -> State1
            end;
        {socket_closed, _Reason} -> State;
        {wfcli_player, Ref, Snapshot} ->
            State1 = send_subscription_update(Ref, Snapshot, State),
            connection_loop(State1);
        {wfcli_daemon, Ref, Reply} ->
            State1 = send_service_reply(Ref, Reply, State),
            connection_loop(State1);
        {wfcli_market_account, Ref, Reply} ->
            State1 = send_market_account_reply(Ref, Reply, State),
            connection_loop(State1);
        {wfcli_market_presence, Ref, Snapshot} ->
            State1 = send_market_presence_event(Ref, Snapshot, State),
            connection_loop(State1);
        {local_request_result, Token, Id, Dataset, Reply} ->
            State1 = send_local_request_result(Token, Id, Dataset, Reply, State),
            connection_loop(State1);
        local_worker_permit ->
            Waiting = max(0, maps:get(worker_permit_requests, State) - 1),
            State1 = schedule_local_workers(
                       State#{worker_permits => maps:get(worker_permits, State) + 1,
                              worker_permit_requests => Waiting}),
            connection_loop(State1);
        {daemon_command, Command} ->
            send_json(maps:get(socket, State),
                      #{<<"event">> => <<"command">>, <<"data">> => Command}),
            connection_loop(State);
        {'DOWN', ReaderMonitor, process, _Reader, Reason} ->
            case Reason of
                normal -> State;
                _ -> exit({socket_reader_failed, Reason})
            end;
        {'DOWN', Monitor, process, _Worker, Reason} ->
            State1 = local_request_down(Monitor, Reason, State),
            connection_loop(State1);
        shutdown -> State;
        _Message -> connection_loop(State)
    end.

socket_reader(Socket, Handler) ->
    case socket:recv(Socket, 0) of
        {ok, <<>>} -> Handler ! {socket_closed, closed};
        {ok, Data} -> Handler ! {socket_data, Data}, socket_reader(Socket, Handler);
        {error, Reason} -> Handler ! {socket_closed, Reason}
    end.

consume_data(Data, State) ->
    Buffer = <<(maps:get(buffer, State))/binary, Data/binary>>,
    case byte_size(Buffer) > ?MAX_BUFFER_BYTES of
        true -> {stop, message_too_large, State};
        false ->
            Parts = binary:split(Buffer, <<"\n">>, [global]),
            {Lines, Rest} = split_last(Parts),
            case consume_lines(Lines, State#{buffer => Rest}) of
                {ok, State1} -> {ok, State1};
                Stop -> Stop
            end
    end.

split_last(Parts) ->
    [Rest | Reversed] = lists:reverse(Parts),
    {lists:reverse(Reversed), Rest}.

consume_lines([], State) -> {ok, State};
consume_lines([<<>> | Rest], State) -> consume_lines(Rest, State);
consume_lines([Line | Rest], State) ->
    case wfcli_local_protocol:decode(Line) of
        {ok, Request} ->
            case handle_request(Request, State) of
                {ok, State1} -> consume_lines(Rest, State1);
                Stop -> Stop
            end;
        {error, Reason} ->
            send_error(maps:get(socket, State), 0, Reason),
            consume_lines(Rest, State)
    end.

handle_request(#{<<"op">> := <<"hello">>} = Request, State) ->
    Id = request_id(Request),
    Protocol = maps:get(<<"protocol">>, Request, undefined),
    Compatible = Protocol =:= wfcli_local_protocol:protocol_version(),
    Client = maps:get(<<"client">>, Request, <<"unknown">>),
    ClientInfo = #{client => Client,
                   version => maps:get(<<"version">>, Request, undefined),
                   os_pid => valid_os_pid(maps:get(<<"pid">>, Request, undefined)),
                   mode => valid_client_mode(maps:get(<<"mode">>, Request, undefined))},
    Reply = (daemon_identity())#{
        <<"id">> => Id,
        <<"ok">> => Compatible,
        <<"compatible">> => Compatible,
        <<"protocol">> => wfcli_local_protocol:protocol_version(),
        <<"capabilities">> => [<<"dataset.get">>, <<"dataset.subscribe">>,
                               <<"player.publish">>, <<"market.quote">>,
                               <<"market.quote.variant">>,
                               <<"market.resolve">>, <<"market.describe">>,
                               <<"market.account">>, <<"market.orders">>,
                               <<"market.presence">>,
                               <<"relic.context">>,
                               <<"relic.planner">>,
                               <<"relic.recommendations">>,
                               <<"worldstate.activity">>,
                               <<"player.foundry">>,
                               <<"player.inventory">>,
                               <<"player.mastery">>,
                               <<"notifications.fissures">>,
                               <<"asset.resolve">>,
                               <<"asset.cache">>,
                               <<"companion.command">>]
    },
    send_json(maps:get(socket, State), Reply),
    case Compatible of
        true -> maps:get(parent, State) ! {client_identified, self(), ClientInfo};
        false -> ok
    end,
    {ok, State#{hello => Compatible}};
handle_request(Request, State = #{hello := false}) ->
    send_error(maps:get(socket, State), request_id(Request), hello_required),
    {ok, State};
handle_request(#{<<"op">> := <<"get">>, <<"dataset">> := Dataset} = Request, State) ->
    Id = request_id(Request),
    {ok, start_local_request(Id, Dataset, fun() -> dataset_snapshot(Dataset) end, State)};
handle_request(#{<<"op">> := <<"foundry_view">>} = Request, State) ->
    Id = request_id(Request),
    {ok, start_local_request(Id, <<"foundry">>,
                             fun wfcli_player_views:foundry/0, State)};
handle_request(#{<<"op">> := <<"inventory_view">>} = Request, State) ->
    Id = request_id(Request),
    {ok, start_local_request(Id, <<"inventory">>,
                             fun wfcli_player_views:inventory/0, State)};
handle_request(#{<<"op">> := <<"mastery_view">>} = Request, State) ->
    Id = request_id(Request),
    {ok, start_local_request(Id, <<"mastery">>,
                             fun wfcli_player_views:mastery/0, State)};
handle_request(#{<<"op">> := <<"activity_view">>} = Request, State) ->
    Id = request_id(Request),
    WorldstateRequest = #{source => worldstate, mode => list,
                          opts => #{ttl => 60, resolve_items => true}},
    case wfcli_worldstate_service:submit(self(), WorldstateRequest) of
        {ok, Ref} ->
            Refs = maps:get(activity_refs, State),
            {ok, State#{activity_refs => Refs#{Ref => Id}}};
        {error, Reason} ->
            send_error(maps:get(socket, State), Id, Reason),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"notification_settings">>} = Request, State) ->
    send_ok(maps:get(socket, State), request_id(Request), <<"notifications">>,
            wfcli_notification_service:settings()),
    {ok, State};
handle_request(#{<<"op">> := <<"notification_settings_set">>,
                 <<"fissures">> := Fissures} = Request, State) when is_map(Fissures) ->
    case wfcli_notification_service:update(#{<<"fissures">> => Fissures}) of
        {ok, Settings} ->
            send_ok(maps:get(socket, State), request_id(Request),
                    <<"notifications">>, Settings);
        {error, Reason} ->
            send_error(maps:get(socket, State), request_id(Request), Reason)
    end,
    {ok, State};
handle_request(#{<<"op">> := <<"notification_settings_set">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request),
               invalid_notification_settings),
    {ok, State};
handle_request(#{<<"op">> := <<"subscribe">>, <<"dataset">> := <<"player">>} = Request,
               State) ->
    subscribe_player(request_id(Request), State);
handle_request(#{<<"op">> := <<"unsubscribe">>, <<"subscription">> := Id} = Request,
               State) when is_integer(Id) ->
    State1 = unsubscribe_player(Id, State),
    send_json(maps:get(socket, State),
              #{<<"id">> => request_id(Request), <<"ok">> => true}),
    {ok, State1};
handle_request(#{<<"op">> := <<"publish">>, <<"dataset">> := <<"player">>,
                 <<"source">> := Source, <<"data">> := Data} = Request, State)
  when is_binary(Source), is_map(Data) ->
    case wfcli_player_service:publish(Source, Data) of
        {ok, Snapshot} ->
            send_json(maps:get(socket, State),
                      #{<<"id">> => request_id(Request), <<"ok">> => true,
                        <<"revision">> => maps:get(revision, Snapshot)});
        {error, Reason} -> send_error(maps:get(socket, State), request_id(Request), Reason)
    end,
    {ok, State};
handle_request(#{<<"op">> := <<"market_resolve">>} = Request, State) ->
    Id = request_id(Request),
    Labels = maps:get(<<"labels">>, Request, undefined),
    Limit = maps:get(<<"limit">>, Request, undefined),
    case {valid_market_labels(Labels), valid_market_limit(Limit)} of
        {false, _} ->
            send_error(maps:get(socket, State), Id, invalid_market_labels),
            {ok, State};
        {true, false} ->
            send_error(maps:get(socket, State), Id, invalid_market_limit),
            {ok, State};
        {true, true} ->
            MarketRequest = #{source => market, action => resolve_labels,
                              labels => Labels, limit => Limit},
            case wfcli_market_service:submit(self(), MarketRequest) of
                {ok, Ref} ->
                    Refs = maps:get(market_refs, State),
                    {ok, State#{market_refs => Refs#{Ref => Id}}};
                {error, Reason} ->
                    send_error(maps:get(socket, State), Id, Reason),
                    {ok, State}
            end
    end;
handle_request(#{<<"op">> := <<"market_quote">>, <<"items">> := Items} = Request, State)
  when is_list(Items), length(Items) =< 100 ->
    Id = request_id(Request),
    Ttl = maps:get(<<"ttl">>, Request, 60),
    case is_integer(Ttl) andalso Ttl >= 60 of
        true ->
            MarketRequest = #{source => market, action => quote_items, items => Items,
                              ttl => Ttl,
                              cache_only =>
                                  maps:get(<<"cache_only">>, Request, false) =:= true,
                              refresh => maps:get(<<"refresh">>, Request, false) =:= true},
            case wfcli_market_service:submit(self(), MarketRequest) of
                {ok, Ref} ->
                    Refs = maps:get(market_refs, State),
                    {ok, State#{market_refs => Refs#{Ref => Id}}};
                {error, Reason} ->
                    send_error(maps:get(socket, State), Id, Reason),
                    {ok, State}
            end;
        false ->
            send_error(maps:get(socket, State), Id, invalid_market_ttl),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"market_quote">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request), invalid_market_items),
    {ok, State};
handle_request(#{<<"op">> := <<"market_quote_variant">>, <<"item">> := Item,
                 <<"filters">> := Filters} = Request, State)
  when is_binary(Item), byte_size(Item) > 0, is_map(Filters) ->
    case market_variant_filters(Filters) of
        {ok, Normalized} ->
            submit_market(request_id(Request),
                          #{source => market, action => quote_variant,
                            item => Item, filters => Normalized, ttl => 60,
                            refresh => maps:get(<<"refresh">>, Request, false) =:= true},
                          State);
        error ->
            send_error(maps:get(socket, State), request_id(Request),
                       invalid_market_quote_filters),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"market_quote_variant">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request),
               invalid_market_quote_filters),
    {ok, State};
handle_request(#{<<"op">> := <<"market_describe">>,
                 <<"items">> := Items} = Request, State)
  when is_list(Items), length(Items) =< 100 ->
    case valid_market_items(Items) of
        true -> submit_market(
                  request_id(Request),
                  #{source => market, action => describe_items, items => Items}, State);
        false ->
            send_error(maps:get(socket, State), request_id(Request), invalid_market_items),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"market_describe">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request), invalid_market_items),
    {ok, State};
handle_request(#{<<"op">> := <<"market_account">>} = Request, State) ->
    submit_market_account(request_id(Request), #{action => snapshot},
                          ensure_market_presence_subscription(State));
handle_request(#{<<"op">> := <<"market_presence">>} = Request, State) ->
    State1 = ensure_market_presence_subscription(State),
    send_ok(maps:get(socket, State1), request_id(Request), <<"market_presence">>,
            wfcli_market_presence_service:snapshot()),
    {ok, State1};
handle_request(#{<<"op">> := <<"market_presence_set">>,
                 <<"mode">> := Mode} = Request, State) when is_binary(Mode) ->
    State1 = ensure_market_presence_subscription(State),
    case wfcli_market_presence_service:set_mode(Mode) of
        {ok, Presence} ->
            send_ok(maps:get(socket, State1), request_id(Request),
                    <<"market_presence">>, Presence);
        {error, Reason} -> send_error(maps:get(socket, State1), request_id(Request), Reason)
    end,
    {ok, State1};
handle_request(#{<<"op">> := <<"market_presence_set">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request),
               invalid_market_presence_mode),
    {ok, State};
handle_request(#{<<"op">> := <<"market_login">>, <<"email">> := Email,
                 <<"password">> := Password} = Request, State)
  when is_binary(Email), byte_size(Email) > 0, byte_size(Email) =< 256,
       is_binary(Password), byte_size(Password) > 0, byte_size(Password) =< 256 ->
    submit_market_account(request_id(Request),
                          #{action => login, email => Email, password => Password}, State);
handle_request(#{<<"op">> := <<"market_login">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request), invalid_market_credentials),
    {ok, State};
handle_request(#{<<"op">> := <<"market_logout">>} = Request, State) ->
    submit_market_account(request_id(Request), #{action => logout}, State);
handle_request(#{<<"op">> := <<"market_order_create">>,
                 <<"order">> := Order} = Request, State) when is_map(Order) ->
    case valid_market_order(Order) of
        true -> submit_market_account(request_id(Request),
                                      #{action => create_order, order => Order}, State);
        false ->
            send_error(maps:get(socket, State), request_id(Request), invalid_market_order),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"market_order_update">>, <<"order_id">> := OrderId,
                 <<"patch">> := Patch} = Request, State)
  when is_binary(OrderId), byte_size(OrderId) > 0, is_map(Patch) ->
    case valid_market_order_patch(Patch) of
        true -> submit_market_account(request_id(Request),
                                      #{action => update_order, id => OrderId,
                                        patch => Patch}, State);
        false ->
            send_error(maps:get(socket, State), request_id(Request),
                       invalid_market_order_patch),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"market_order_delete">>,
                 <<"order_id">> := OrderId} = Request, State)
  when is_binary(OrderId), byte_size(OrderId) > 0 ->
    submit_market_account(request_id(Request),
                          #{action => delete_order, id => OrderId}, State);
handle_request(#{<<"op">> := <<"market_order_close">>, <<"order_id">> := OrderId,
                 <<"quantity">> := Quantity} = Request, State)
  when is_binary(OrderId), byte_size(OrderId) > 0,
       is_integer(Quantity), Quantity >= 1, Quantity =< 9999 ->
    submit_market_account(request_id(Request),
                          #{action => close_order, id => OrderId,
                            quantity => Quantity}, State);
handle_request(#{<<"op">> := <<"market_orders_visibility">>,
                 <<"visible">> := Visible} = Request, State) when is_boolean(Visible) ->
    Type = maps:get(<<"type">>, Request, undefined),
    case Type =:= undefined orelse Type =:= <<"buy">> orelse Type =:= <<"sell">> of
        true -> submit_market_account(request_id(Request),
                                      #{action => set_visibility, visible => Visible,
                                        type => Type}, State);
        false ->
            send_error(maps:get(socket, State), request_id(Request),
                       invalid_market_order_type),
            {ok, State}
    end;
handle_request(#{<<"op">> := MarketOperation} = Request, State)
  when MarketOperation =:= <<"market_order_create">>;
       MarketOperation =:= <<"market_order_update">>;
       MarketOperation =:= <<"market_order_delete">>;
       MarketOperation =:= <<"market_order_close">>;
       MarketOperation =:= <<"market_orders_visibility">> ->
    send_error(maps:get(socket, State), request_id(Request), invalid_market_order_request),
    {ok, State};
handle_request(#{<<"op">> := <<"relic_context">>,
                 <<"items">> := Items} = Request, State)
  when is_list(Items), length(Items) =< 8 ->
    Id = request_id(Request),
    MarketRequest = #{source => market, action => relic_context, items => Items,
                      ttl => 60, refresh => false},
    case wfcli_market_service:submit(self(), MarketRequest) of
        {ok, Ref} ->
            Refs = maps:get(market_refs, State),
            {ok, State#{market_refs => Refs#{Ref => Id}}};
        {error, Reason} ->
            send_error(maps:get(socket, State), Id, Reason),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"relic_context">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request), invalid_relic_items),
    {ok, State};
handle_request(#{<<"op">> := <<"relic_recommendations">>,
                 <<"era">> := Era} = Request, State) when is_binary(Era) ->
    Id = request_id(Request),
    case relic_limit(Request, 32) of
        {ok, Limit} ->
            MarketRequest = #{source => market, action => relic_recommendations,
                              era => Era, view => recommendations, limit => Limit,
                              only_owned => true,
                              fetch_prices =>
                                  maps:get(<<"fetch_prices">>, Request, false) =:= true},
            case wfcli_market_service:submit(self(), MarketRequest) of
                {ok, Ref} ->
                    Refs = maps:get(market_refs, State),
                    {ok, State#{market_refs => Refs#{Ref => Id}}};
                {error, Reason} ->
                    send_error(maps:get(socket, State), Id, Reason),
                    {ok, State}
            end;
        error ->
            send_error(maps:get(socket, State), Id, invalid_relic_limit),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"relic_recommendations">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request), invalid_relic_era),
    {ok, State};
handle_request(#{<<"op">> := <<"relic_planner">>,
                 <<"era">> := Era} = Request, State) when is_binary(Era) ->
    Id = request_id(Request),
    case relic_limit(Request, all) of
        {ok, Limit} ->
            MarketRequest = #{source => market, action => relic_recommendations,
                              era => Era, view => planner, limit => Limit,
                              only_owned =>
                                  maps:get(<<"only_owned">>, Request, true) =:= true,
                              fetch_prices =>
                                  maps:get(<<"fetch_prices">>, Request, false) =:= true},
            case wfcli_market_service:submit(self(), MarketRequest) of
                {ok, Ref} ->
                    Refs = maps:get(market_refs, State),
                    {ok, State#{market_refs => Refs#{Ref => Id}}};
                {error, Reason} ->
                    send_error(maps:get(socket, State), Id, Reason),
                    {ok, State}
            end;
        error ->
            send_error(maps:get(socket, State), Id, invalid_relic_limit),
            {ok, State}
    end;
handle_request(#{<<"op">> := <<"relic_planner">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request), invalid_relic_era),
    {ok, State};
handle_request(#{<<"op">> := <<"asset_resolve">>,
                 <<"assets">> := Assets} = Request, State)
  when is_list(Assets), length(Assets) =< 64 ->
    Id = request_id(Request),
    Work = fun() ->
        case wfcli_asset_service:resolve(Assets) of
            {ok, Results} -> {ok, #{<<"assets">> => Results}};
            {error, _Reason} = Error -> Error
        end
    end,
    {ok, start_local_request(Id, <<"assets">>, Work, State)};
handle_request(#{<<"op">> := <<"asset_resolve">>} = Request, State) ->
    send_error(maps:get(socket, State), request_id(Request), invalid_assets),
    {ok, State};
handle_request(#{<<"op">> := <<"asset_cache_status">>} = Request, State) ->
    Id = request_id(Request),
    Work = fun() ->
        {ok, asset_cache_data(wfcli_asset_service:status())}
    end,
    {ok, start_local_request(Id, <<"asset_cache">>, Work, State)};
handle_request(#{<<"op">> := <<"asset_cache_clear">>} = Request, State) ->
    Id = request_id(Request),
    Work = fun() ->
        case wfcli_asset_service:clear() of
            {ok, Status} -> {ok, asset_cache_data(Status)};
            {error, _Reason} = Error -> Error
        end
    end,
    {ok, start_local_request(Id, <<"asset_cache">>, Work, State)};
handle_request(Request, State) ->
    send_error(maps:get(socket, State), request_id(Request), unsupported_request),
    {ok, State}.

valid_market_labels(Labels)
  when is_list(Labels), length(Labels) =< ?MAX_MARKET_RESOLVE_LABELS ->
    lists:all(fun(Label) -> is_binary(Label) andalso byte_size(Label) > 0 end, Labels);
valid_market_labels(_Labels) -> false.

valid_market_limit(Limit) ->
    is_integer(Limit) andalso Limit >= 1 andalso Limit =< ?MAX_MARKET_RESOLVE_LIMIT.

valid_market_items(Items) ->
    lists:all(fun(Item) -> is_binary(Item) andalso byte_size(Item) > 0 end, Items).

asset_cache_data(Status) ->
    #{<<"cache_root">> => wfcli_text:to_binary(maps:get(cache_root, Status)),
      <<"entries">> => maps:get(entries, Status),
      <<"objects">> => maps:get(objects, Status),
      <<"bytes">> => maps:get(bytes, Status),
      <<"queued">> => maps:get(queued, Status),
      <<"pending">> => maps:get(pending, Status),
      <<"fetching">> => maps:get(fetching, Status),
      <<"waiting_calls">> => maps:get(waiting_calls, Status)}.

market_variant_filters(Filters) ->
    Allowed = #{<<"rank">> => {rank, 100}, <<"charges">> => {charges, 100000},
                <<"amberStars">> => {amber_stars, 100},
                <<"cyanStars">> => {cyan_stars, 100}},
    maps:fold(
      fun(_Key, _Value, error) -> error;
         (<<"subtype">>, Value, {ok, Acc}) when is_binary(Value),
                                                byte_size(Value) > 0,
                                                byte_size(Value) =< 128 ->
              {ok, Acc#{subtype => Value}};
         (Key, Value, {ok, Acc}) when is_integer(Value), Value >= 0 ->
              case maps:get(Key, Allowed, undefined) of
                  {Name, Max} when Value =< Max -> {ok, Acc#{Name => Value}};
                  _ -> error
              end;
         (_Key, _Value, {ok, _Acc}) -> error
      end,
      {ok, #{}}, Filters).

valid_market_order(Order) ->
    valid_nonempty_binary(maps:get(<<"itemId">>, Order, undefined)) andalso
    valid_market_order_type(maps:get(<<"type">>, Order, undefined)) andalso
    valid_integer(maps:get(<<"platinum">>, Order, undefined), 1, 900000) andalso
    valid_integer(maps:get(<<"quantity">>, Order, undefined), 1, 9999) andalso
    valid_optional_boolean(maps:get(<<"visible">>, Order, undefined)) andalso
    valid_order_options(Order).

valid_market_order_patch(Patch) when map_size(Patch) > 0 ->
    Allowed = [<<"platinum">>, <<"quantity">>, <<"visible">>, <<"perTrade">>,
               <<"rank">>, <<"charges">>, <<"subtype">>, <<"amberStars">>,
               <<"cyanStars">>],
    lists:all(fun(Key) -> lists:member(Key, Allowed) end, maps:keys(Patch)) andalso
    valid_optional_integer(maps:get(<<"platinum">>, Patch, undefined), 1, 900000) andalso
    valid_optional_integer(maps:get(<<"quantity">>, Patch, undefined), 1, 9999) andalso
    valid_optional_boolean(maps:get(<<"visible">>, Patch, undefined)) andalso
    valid_order_options(Patch);
valid_market_order_patch(_Patch) -> false.

valid_order_options(Order) ->
    valid_optional_integer(maps:get(<<"perTrade">>, Order, undefined), 1, 6) andalso
    valid_optional_integer(maps:get(<<"rank">>, Order, undefined), 0, 100) andalso
    valid_optional_integer(maps:get(<<"charges">>, Order, undefined), 0, 100000) andalso
    valid_optional_integer(maps:get(<<"amberStars">>, Order, undefined), 0, 100) andalso
    valid_optional_integer(maps:get(<<"cyanStars">>, Order, undefined), 0, 100) andalso
    valid_optional_binary(maps:get(<<"subtype">>, Order, undefined)).

valid_market_order_type(<<"buy">>) -> true;
valid_market_order_type(<<"sell">>) -> true;
valid_market_order_type(_Type) -> false.

valid_nonempty_binary(Value) -> is_binary(Value) andalso byte_size(Value) > 0.
valid_optional_binary(undefined) -> true;
valid_optional_binary(Value) -> valid_nonempty_binary(Value).
valid_optional_boolean(undefined) -> true;
valid_optional_boolean(Value) -> is_boolean(Value).
valid_integer(Value, Min, Max) -> is_integer(Value) andalso Value >= Min andalso Value =< Max.
valid_optional_integer(undefined, _Min, _Max) -> true;
valid_optional_integer(Value, Min, Max) -> valid_integer(Value, Min, Max).

relic_limit(Request, Default) ->
    case maps:get(<<"limit">>, Request, Default) of
        <<"all">> -> {ok, all};
        Limit when is_integer(Limit), Limit >= 1, Limit =< 1000 -> {ok, Limit};
        _ -> error
    end.

subscribe_player(Id, State) when is_integer(Id) ->
    State1 = unsubscribe_player(Id, State),
    case wfcli_player_service:subscribe(self()) of
        {ok, Ref, Snapshot} ->
            send_ok(maps:get(socket, State1), Id, <<"player">>, Snapshot),
            Subscriptions = maps:get(subscriptions, State1),
            Refs = maps:get(refs, State1),
            {ok, State1#{subscriptions => Subscriptions#{Id => Ref}, refs => Refs#{Ref => Id}}};
        {error, Reason} ->
            send_error(maps:get(socket, State1), Id, Reason),
            {ok, State1}
    end;
subscribe_player(_Id, State) ->
    send_error(maps:get(socket, State), 0, invalid_request_id),
    {ok, State}.

unsubscribe_player(Id, State) ->
    case maps:take(Id, maps:get(subscriptions, State)) of
        error -> State;
        {Ref, Subscriptions} ->
            _ = wfcli_player_service:unsubscribe(Ref),
            Refs = maps:remove(Ref, maps:get(refs, State)),
            State#{subscriptions => Subscriptions, refs => Refs}
    end.

send_subscription_update(Ref, Snapshot, State) ->
    case maps:get(Ref, maps:get(refs, State), undefined) of
        undefined -> State;
        Id ->
            send_json(maps:get(socket, State),
                      #{<<"event">> => <<"dataset">>,
                        <<"subscription">> => Id,
                        <<"dataset">> => <<"player">>,
                        <<"data">> => json_snapshot(Snapshot)}),
            State
    end.

send_service_reply(Ref, Reply, State) ->
    case maps:take(Ref, maps:get(activity_refs, State, #{})) of
        error -> send_market_reply(Ref, Reply, State);
        {Id, ActivityRefs} ->
            Projected = case Reply of
                {ok, WorldstateData} -> wfcli_activity_view:project(WorldstateData);
                {error, _Reason} = Error -> Error
            end,
            case Projected of
                {ok, ActivityData} ->
                    send_ok(maps:get(socket, State), Id, <<"activity">>, ActivityData);
                {error, Reason} -> send_error(maps:get(socket, State), Id, Reason)
            end,
            State#{activity_refs => ActivityRefs}
    end.

send_market_reply(Ref, Reply, State) ->
    case maps:take(Ref, maps:get(market_refs, State, #{})) of
        error -> State;
        {Id, MarketRefs} ->
            case Reply of
                {ok, Data} -> send_ok(maps:get(socket, State), Id, <<"market">>, Data);
                {error, Reason} -> send_error(maps:get(socket, State), Id, Reason)
            end,
            State#{market_refs => MarketRefs}
    end.

send_market_account_reply(Ref, Reply, State) ->
    case maps:take(Ref, maps:get(market_account_refs, State, #{})) of
        error -> State;
        {Id, AccountRefs} ->
            case Reply of
                {ok, Data} ->
                    Account = Data#{<<"presence">> =>
                                        wfcli_market_presence_service:snapshot()},
                    send_ok(maps:get(socket, State), Id,
                            <<"market_account">>, Account);
                {error, Reason} -> send_error(maps:get(socket, State), Id, Reason)
            end,
            State#{market_account_refs => AccountRefs}
    end.

send_market_presence_event(Ref, Snapshot, State) ->
    case maps:get(market_presence_ref, State, undefined) of
        Ref ->
            send_json(maps:get(socket, State),
                      #{<<"event">> => <<"market_presence">>,
                        <<"data">> => Snapshot}),
            State;
        _ -> State
    end.

ensure_market_presence_subscription(State = #{market_presence_ref := Ref})
  when is_reference(Ref) -> State;
ensure_market_presence_subscription(State) ->
    case wfcli_market_presence_service:subscribe(self()) of
        {ok, Ref, _Snapshot} -> State#{market_presence_ref => Ref};
        _ -> State
    end.

submit_market(Id, MarketRequest, State) ->
    case wfcli_market_service:submit(self(), MarketRequest) of
        {ok, Ref} ->
            Refs = maps:get(market_refs, State),
            {ok, State#{market_refs => Refs#{Ref => Id}}};
        {error, Reason} ->
            send_error(maps:get(socket, State), Id, Reason),
            {ok, State}
    end.

submit_market_account(Id, AccountRequest, State) ->
    case wfcli_market_account_service:submit(self(), AccountRequest) of
        {ok, Ref} ->
            Refs = maps:get(market_account_refs, State),
            {ok, State#{market_account_refs => Refs#{Ref => Id}}};
        {error, Reason} ->
            send_error(maps:get(socket, State), Id, Reason),
            {ok, State}
    end.

start_local_request(Id, Dataset, Work, State) ->
    Pending = queue:in({Id, Dataset, Work}, maps:get(worker_queue, State)),
    schedule_local_workers(State#{worker_queue => Pending}).

start_local_worker(Id, Dataset, Work, State) ->
    Parent = self(),
    Token = make_ref(),
    {Pid, Monitor} = spawn_opt(fun() ->
        Reply = try Work()
                catch Class:Reason:Stack ->
                    logger:error("local API request failed: ~p:~p~n~p",
                                 [Class, Reason, Stack]),
                    {error, {local_request_failed, Class, Reason}}
                end,
        Parent ! {local_request_result, Token, Id, Dataset, Reply}
    end, [link, monitor]),
    Worker = #{pid => Pid, monitor => Monitor, id => Id},
    Workers = maps:get(workers, State, #{}),
    State#{workers => Workers#{Token => Worker}}.

send_local_request_result(Token, Id, Dataset, Reply, State) ->
    case maps:take(Token, maps:get(workers, State, #{})) of
        error -> State;
        {#{monitor := Monitor}, Workers} ->
            erlang:demonitor(Monitor, [flush]),
            release_global_local_worker(),
            case Reply of
                {ok, Data} -> send_ok(maps:get(socket, State), Id, Dataset, Data);
                {error, Reason} -> send_error(maps:get(socket, State), Id, Reason)
            end,
            schedule_local_workers(State#{workers => Workers})
    end.

local_request_down(Monitor, Reason, State) ->
    case [{Token, Worker} ||
             {Token, Worker = #{monitor := WorkerMonitor}} <-
                 maps:to_list(maps:get(workers, State, #{})),
             WorkerMonitor =:= Monitor] of
        [{Token, #{id := Id}}] ->
            release_global_local_worker(),
            send_error(maps:get(socket, State), Id, {local_request_down, Reason}),
            schedule_local_workers(
              State#{workers => maps:remove(Token, maps:get(workers, State))});
        [] -> State
    end.

schedule_local_workers(State) ->
    State1 = start_permitted_local_workers(State),
    Slots = maps:get(max_workers, State1) - map_size(maps:get(workers, State1)),
    Needed0 = min(Slots, queue:len(maps:get(worker_queue, State1))),
    Reserved = maps:get(worker_permits, State1) +
               maps:get(worker_permit_requests, State1),
    case max(0, Needed0 - Reserved) of
        0 -> State1;
        Needed ->
            {Granted, Queued} = gen_server:call(
                                  ?SERVER, {acquire_local_workers, self(), Needed}),
            start_permitted_local_workers(
              State1#{worker_permits => maps:get(worker_permits, State1) + Granted,
                      worker_permit_requests =>
                          maps:get(worker_permit_requests, State1) + Queued})
    end.

start_permitted_local_workers(State) ->
    CanStart = maps:get(worker_permits, State) > 0 andalso
               map_size(maps:get(workers, State)) < maps:get(max_workers, State),
    case {CanStart, queue:out(maps:get(worker_queue, State))} of
        {true, {{value, {Id, Dataset, Work}}, Queue}} ->
            State1 = start_local_worker(
                       Id, Dataset, Work,
                       State#{worker_queue => Queue,
                              worker_permits => maps:get(worker_permits, State) - 1}),
            start_permitted_local_workers(State1);
        {_, {empty, Queue}} -> State#{worker_queue => Queue};
        _ -> State
    end.

release_global_local_worker() ->
    gen_server:cast(?SERVER, {release_local_worker, self()}).

local_worker_limit() ->
    case application:get_env(wfdaemon, local_request_workers, ?DEFAULT_REQUEST_WORKERS) of
        Count when is_integer(Count), Count > 0 -> Count;
        _ -> ?DEFAULT_REQUEST_WORKERS
    end.

dataset_snapshot(<<"player">>) ->
    {ok, json_snapshot(wfcli_player_service:snapshot())};
dataset_snapshot(<<"daemon">>) ->
    {ok, daemon_identity()};
dataset_snapshot(Dataset) ->
    {error, {unsupported_dataset, Dataset}}.

json_snapshot(Snapshot) ->
    #{<<"revision">> => maps:get(revision, Snapshot),
      <<"updated_at">> => nullable(maps:get(updated_at, Snapshot)),
      <<"data">> => maps:get(data, Snapshot)}.

daemon_identity() ->
    Build = case wfcli_hot_update:current_build_identity() of
        {ok, Identity} -> Identity;
        {error, _Reason} -> null
    end,
    Version = case application:get_key(wfdaemon, vsn) of
        {ok, Vsn} -> list_to_binary(Vsn);
        undefined -> <<"unknown">>
    end,
    #{<<"status">> => <<"running">>,
      <<"node">> => atom_to_binary(node()),
      <<"version">> => Version,
      <<"otp">> => list_to_binary(erlang:system_info(otp_release)),
      <<"build">> => Build}.

request_id(Request) ->
    case maps:get(<<"id">>, Request, 0) of
        Id when is_integer(Id), Id >= 0 -> Id;
        _ -> 0
    end.

send_ok(Socket, Id, Dataset, Snapshot) ->
    send_json(Socket, #{<<"id">> => Id, <<"ok">> => true,
                        <<"dataset">> => Dataset, <<"data">> => Snapshot}).

send_error(Socket, Id, Reason) ->
    send_json(Socket, #{<<"id">> => Id, <<"ok">> => false,
                        <<"error">> => format_reason(Reason)}).

send_json(Socket, Map) ->
    case socket:send(Socket, wfcli_local_protocol:encode(Map)) of
        ok -> ok;
        {error, closed} -> ok;
        {error, Reason} -> exit({socket_send_failed, Reason})
    end.

format_reason(Reason) when is_binary(Reason) -> Reason;
format_reason(Reason) when is_atom(Reason) -> atom_to_binary(Reason);
format_reason(Reason) -> iolist_to_binary(io_lib:format("~p", [Reason])).

nullable(undefined) -> null;
nullable(Value) -> Value.

cleanup_connection(State) ->
    maps:foreach(fun(_Id, Ref) -> wfcli_player_service:unsubscribe(Ref) end,
                 maps:get(subscriptions, State, #{})),
    maps:foreach(
      fun(_Token, #{pid := Pid, monitor := Monitor}) ->
          exit(Pid, shutdown),
          erlang:demonitor(Monitor, [flush])
      end,
      maps:get(workers, State, #{})),
    case maps:get(market_presence_ref, State, undefined) of
        Ref when is_reference(Ref) ->
            _ = wfcli_market_presence_service:unsubscribe(Ref);
        _ -> ok
    end,
    exit(maps:get(reader, State), shutdown),
    erlang:demonitor(maps:get(reader_monitor, State), [flush]),
    ok.

update_client_activity(_Pid, Previous, Client) when Previous =:= Client -> ok;
update_client_activity(Pid, Previous, Client) ->
    set_client_activity(is_active_client(Previous), is_active_client(Client)),
    set_gui_activity(Pid, Previous =:= <<"wfgui">>, Client =:= <<"wfgui">>).

set_client_activity(true, false) ->
    wfcli_worldstate_service:activity_end();
set_client_activity(false, true) ->
    wfcli_worldstate_service:activity_start();
set_client_activity(_Previous, _Client) -> ok.

release_client_activity(Pid, #{client := Client}) ->
    release_activity(is_active_client(Client)),
    set_gui_activity(Pid, Client =:= <<"wfgui">>, false);
release_client_activity(_Pid, _Info) -> ok.

release_activity(true) ->
    wfcli_worldstate_service:activity_end();
release_activity(false) -> ok.

is_active_client(<<"wfcompanion">>) -> true;
is_active_client(<<"wfgui">>) -> true;
is_active_client(_Client) -> false.

set_gui_activity(_Pid, Previous, Current) when Previous =:= Current -> ok;
set_gui_activity(Pid, false, true) ->
    wfcli_notification_service:gui_connected(Pid);
set_gui_activity(Pid, true, false) ->
    wfcli_notification_service:gui_disconnected(Pid).

valid_os_pid(Pid) when is_integer(Pid), Pid > 0 -> Pid;
valid_os_pid(_Pid) -> undefined.

valid_client_mode(<<"standalone">>) -> <<"standalone">>;
valid_client_mode(<<"launch">>) -> <<"launch">>;
valid_client_mode(<<"desktop">>) -> <<"desktop">>;
valid_client_mode(_Mode) -> <<"unknown">>.
